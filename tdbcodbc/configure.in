#!/bin/bash -norc
dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during Tcl installation
dnl	to configure the system for the local environment.
#
# RCS: @(#) $Id: configure.in,v 1.47 2007/02/09 19:06:47 hobbs Exp $

#-----------------------------------------------------------------------
# Sample configure.in for Tcl Extensions.  The only places you should
# need to modify this file are marked by the string __CHANGE__
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Set your package name and version numbers here.
#
# This initializes the environment with PACKAGE_NAME and PACKAGE_VERSION
# set as provided.  These will also be added as -D defs in your Makefile
# so you can encode the package version directly into the source files.
#-----------------------------------------------------------------------

AC_REVISION([$Revision: 0.1 $])
AC_INIT([tdbcodbc], [0.2a1])

#--------------------------------------------------------------------
# Call TEA_INIT as the first TEA_ macro to set up initial vars.
# This will define a ${TEA_PLATFORM} variable == "unix" or "windows"
# as well as PKG_LIB_FILE and PKG_STUB_LIB_FILE.
#--------------------------------------------------------------------

AC_CONFIG_AUX_DIR(tclconfig)
TEA_INIT([3.6])

#--------------------------------------------------------------------
# Load the tclConfig.sh file
#--------------------------------------------------------------------

TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG

#--------------------------------------------------------------------
# Load the tkConfig.sh file if necessary (Tk extension)
#--------------------------------------------------------------------

TEA_PATH_TKCONFIG
TEA_LOAD_TKCONFIG

#--------------------------------------------------------------------
# Load the tclooConfig.sh file on Tcl versions where oo is an
# extension.
#--------------------------------------------------------------------

if test "${TCL_MAJOR_VERSION}" -lt 8 ; then
    AC_MSG_ERROR([${PACKAGE_NAME} ${PACKAGE_VERSION} requires Tcl 8.5 or newer.
Found configuration for Tcl ${TCL_VERSION}.])
fi
if test "${TCL_MAJOR_VERSION}" -eq 8 ; then
  if test "${TCL_MINOR_VERSION}" -lt 5 ; then
    AC_MSG_ERROR([${PACKAGE_NAME} ${PACKAGE_VERSION} requires Tcl 8.5 or newer.
Found configuration for Tcl ${TCL_VERSION}.])
  fi
  if test "${TCL_MINOR_VERSION}" -eq 5 ; then
    TEA_PATH_CONFIG(tcloo)
    TEA_LOAD_CONFIG(tcloo)
  else
    TCLOO_CFLAGS='-DUSE_TCLOO_STUBS'
  fi
fi

TEA_PATH_CONFIG(tdbc)
TEA_LOAD_CONFIG(tdbc)

#-----------------------------------------------------------------------
# Handle the --prefix=... option by defaulting to what Tcl gave.
# Must be called after TEA_LOAD_TCLCONFIG and before TEA_SETUP_COMPILER.
#-----------------------------------------------------------------------

TEA_PREFIX

#-----------------------------------------------------------------------
# Standard compiler checks.
# This sets up CC by using the CC env var, or looks for gcc otherwise.
# This also calls AC_PROG_CC, AC_PROG_INSTALL and a few others to create
# the basic setup necessary to compile executables.
#-----------------------------------------------------------------------

TEA_SETUP_COMPILER
AC_C_INLINE
AC_CHECK_TYPE([long long],[
    AC_DEFINE([HAVE_LONG_LONG],[1])
    AC_CHECK_SIZEOF([long long])
],[],[])
AC_CHECK_SIZEOF([long])
AC_DEFINE([DONT_TD_VOID],[1])

#----------------------------------------------------------------------
# Locate ODBC include files, and set ODBC_INCLUDE_SPEC accordingly
#----------------------------------------------------------------------

TDBCODBC_SAVED_CPPFLAGS="${CPPFLAGS}"
AC_ARG_WITH(odbc-includedir,
    AC_HELP_STRING([--with-odbc-includedir],
                   [directory where ODBC header files reside]),
    with_odbcincludedir=${withval})
if test x"${with_odbcincludedir}" != x ; then
    CPPFLAGS="${CPPFLAGS} -I\"${odbc_includedir}\""
    ODBC_INCLUDE_SPEC=-I\"${odbc_includedir}\"
fi
AC_CHECK_HEADER([sql.h], , AC_MSG_ERROR("cannot find a working sql.h"), [
#ifdef _WIN32
#define WIN32_LEAN_AND_MEAN
#include <windows.h>
#undef WIN32_LEAN_AND_MEAN
#endif
])
AC_CHECK_HEADER([odbcinst.h], HAVE_ODBCINST=1, AC_DEFINE(NO_ODBCINST), [
#ifdef _WIN32
#define WIN32_LEAN_AND_MEAN
#include <windows.h>
#undef WIN32_LEAN_AND_MEAN
#endif
#include <sql.h>
])
CPPFLAGS="${TDBCODBC_SAVED_CPPLFAGS}"
TEA_ADD_INCLUDES([${ODBC_INCLUDE_SPEC}])

#----------------------------------------------------------------------
# Locate the ODBC libraries. Right now, these must be on the default
# path.
#----------------------------------------------------------------------

AC_CHECK_LIB([odbc32],[strlen],[
    eval "ODBC_LIB_SPEC=\"-lodbc32\""
], [
    AC_CHECK_LIB([odbc],[strlen],[
        eval "ODBC_LIB_SPEC=\"-lodbc\""
    ], [
        AC_MSG_ERROR([Cannot find an ODBC library])
    ],[$PKG_LIBS])
],[$PKG_LIBS])
TEA_ADD_LIBS([${ODBC_LIB_SPEC}])
AC_CHECK_LIB([odbccp32],[strlen],[
    eval "ODBCINST_LIB_SPEC=\"-lodbccp32\""
], [
   AC_CHECK_LIB([odbccp],[strlen],[
        eval "ODBCINST_LIB_SPEC=\"-lodbccp\""
    ],[
       AC_CHECK_LIB([odbcinst],[strlen],[
            eval "ODBCINST_LIB_SPEC=\"-lodbcinst\""
        ],[],[$PKG_LIBS])
    ],[$PKG_LIBS])
],[$PKG_LIBS])
if test x"${HAVE_ODBCINST}" != x ; then
    TDBCODBC_SAVED_LIBS="${LIBS}"
    LIBS="${ODBCINST_LIB_SPEC} ${ODBC_LIB_SPEC} ${LIBS}"
    AC_MSG_CHECKING([for Unicode interface to odbcinst])
    AC_LINK_IFELSE([
#ifdef _WIN32
#define WIN32_LEAN_AND_MEAN
#include <windows.h>
#undef WIN32_LEAN_AND_MEAN
#endif
#include <sql.h>
#include <odbcinst.h>
int main(int argc, char* argv[]) {
    SQLConfigDataSourceW(NULL, 0, (WCHAR*)NULL, (WCHAR*)NULL);
    return 0;
}
    ],[AC_MSG_RESULT([yes])],[
        AC_MSG_RESULT(no)
        AC_DEFINE(NO_UNICODE_ODBCINST)
    ])
    LIBS="${TDBCODBC_SAVED_LIBS}"
fi
TEA_ADD_LIBS([${ODBCINST_LIB_SPEC}])

#-----------------------------------------------------------------------
# Specify the C source files to compile in TEA_ADD_SOURCES,
# public headers that need to be installed in TEA_ADD_HEADERS,
# stub library C source files to compile in TEA_ADD_STUB_SOURCES,
# and runtime Tcl library files in TEA_ADD_TCL_SOURCES.
# This defines PKG(_STUB)_SOURCES, PKG(_STUB)_OBJECTS, PKG_HEADERS
# and PKG_TCL_SOURCES.
#-----------------------------------------------------------------------

TEA_ADD_SOURCES(generic/tdbcodbc.c)
TEA_ADD_HEADERS()
if test "${TCL_MAJOR_VERSION}" -eq 8 ; then
  if test "${TCL_MINOR_VERSION}" -eq 5 ; then
    TEA_ADD_INCLUDES([${TCLOO_INCLUDE_SPEC}])
    TEA_ADD_LIBS([${TCLOO_STUB_LIB_SPEC}])
  fi
fi
TEA_ADD_CFLAGS([${TCLOO_CFLAGS}])
TEA_ADD_INCLUDES([${TDBC_INCLUDE_SPEC}])
TEA_ADD_LIBS([${TDBC_STUB_LIB_SPEC}])
TEA_ADD_CFLAGS([${TDBC_CFLAGS}])
TEA_ADD_STUB_SOURCES()
TEA_ADD_TCL_SOURCES([library/tdbcodbc.tcl])

#--------------------------------------------------------------------
# A few miscellaneous platform-specific items:
#
# Define a special symbol for Windows (BUILD_sample in this case) so
# that we create the export library with the dll.
#
# Windows creates a few extra files that need to be cleaned up.
# You can add more files to clean if your extension creates any extra
# files.
#
# TEA_ADD_* any platform specific compiler/build info here.
#--------------------------------------------------------------------

# Add pkgIndex.tcl if it is generated in the Makefile instead of ./configure
# and change Makefile.in to move it from CONFIG_CLEAN_FILES to BINARIES var.
#CLEANFILES="pkgIndex.tcl"
AC_DEFINE(BUILD_tdbcodbc, 1, [Build export dll])
if test "${TEA_PLATFORM}" = "windows" ; then
    CLEANFILES="$CLEANFILES *.lib *.dll *.exp *.ilk *.pdb vc*.pch"
else
    # Ensure no empty else clauses
    :
fi
CLEANFILES="$CLEANFILES pkgIndex.tcl"
AC_SUBST(CLEANFILES)

#--------------------------------------------------------------------
# Choose which headers you need.  Extension authors should try very
# hard to only rely on the Tcl public header files.  Internal headers
# contain private data structures and are subject to change without
# notice.
# This MUST be called after TEA_LOAD_TCLCONFIG / TEA_LOAD_TKCONFIG
#--------------------------------------------------------------------

TEA_PUBLIC_TCL_HEADERS

#--------------------------------------------------------------------
# Check whether --enable-threads or --disable-threads was given.
# This auto-enables if Tcl was compiled threaded.
#--------------------------------------------------------------------

TEA_ENABLE_THREADS

#--------------------------------------------------------------------
# The statement below defines a collection of symbols related to
# building as a shared library instead of a static library.
#--------------------------------------------------------------------

TEA_ENABLE_SHARED

#--------------------------------------------------------------------
# This macro figures out what flags to use with the compiler/linker
# when building shared/static debug/optimized objects.  This information
# can be taken from the tclConfig.sh file, but this figures it all out.
#--------------------------------------------------------------------

TEA_CONFIG_CFLAGS

#--------------------------------------------------------------------
# Set the default compiler switches based on the --enable-symbols option.
#--------------------------------------------------------------------

TEA_ENABLE_SYMBOLS

#--------------------------------------------------------------------
# Everyone should be linking against the Tcl stub library.  If you
# can't for some reason, remove this definition.  If you aren't using
# stubs, you also need to modify the SHLIB_LD_LIBS setting below to
# link against the non-stubbed Tcl library.  Add Tk too if necessary.
#--------------------------------------------------------------------

AC_DEFINE(BUILD_tdbcodbc,[1],[Set to 1 when building the tdbcodbc package])
AC_DEFINE(USE_TCL_STUBS, 1, [Use Tcl stubs])
AC_DEFINE(USE_TK_STUBS, 1, [Use Tk stubs])

#--------------------------------------------------------------------
# This macro generates a line to use when building a library.  It
# depends on values set by the TEA_ENABLE_SHARED, TEA_ENABLE_SYMBOLS,
# and TEA_LOAD_TCLCONFIG macros above.
#--------------------------------------------------------------------

TEA_MAKE_LIB
TEAX_VC_MANIFEST

#--------------------------------------------------------------------
# Determine the name of the tclsh and/or wish executables in the
# Tcl and Tk build directories or the location they were installed
# into. These paths are used to support running test cases only,
# the Makefile should not be making use of these paths to generate
# a pkgIndex.tcl file or anything else at extension build time.
#--------------------------------------------------------------------

TEA_PROG_TCLSH

#--------------------------------------------------------------------
# Finally, substitute all of the various values into the Makefile.
# You may alternatively have a special pkgIndex.tcl.in or other files
# which require substituting th AC variables in.  Include these here.
#--------------------------------------------------------------------

CONFIGURE_OUTPUTS="Makefile tdbcConfig.sh config.cache config.log config.status"
AC_SUBST(CONFIGURE_OUTPUTS)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
